# this one is important
SET(CMAKE_SYSTEM_NAME Linux)
#this one not so much
SET(CMAKE_SYSTEM_VERSION 1)

# specify the cross compiler
SET(PL_TOOLS_ROOT /opt/pl-build-tools)
SET(CMAKE_C_COMPILER     ${PL_TOOLS_ROOT}/bin/gcc)
SET(CMAKE_CXX_COMPILER   ${PL_TOOLS_ROOT}/bin/g++)

# where is the target environment
SET(CMAKE_FIND_ROOT_PATH  /opt/pl-build-tools)

# search for programs in the build host directories
SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM BOTH)

# for libraries and headers in the target directories
SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY BOTH)
SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE BOTH)

SET(CMAKE_C_FLAGS "-fPIC ${CMAKE_C_FLAGS}" CACHE STRING "" FORCE)
SET(CMAKE_CXX_FLAGS "-fPIC ${CMAKE_CXX_FLAGS}" CACHE STRING "" FORCE)

SET(PL_RPATH_FLAGS "-Wl,-rpath=\"${PL_TOOLS_ROOT}/lib:${PL_TOOLS_ROOT}/lib64\" -L${PL_TOOLS_ROOT}/lib -L${PL_TOOLS_ROOT}/lib64")
SET(CMAKE_EXE_LINKER_FLAGS "${PL_RPATH_FLAGS} ${CMAKE_EXE_LINKER_FLAGS}" CACHE STRING "" FORCE)
SET(CMAKE_SHARED_LINKER_FLAGS "${PL_RPATH_FLAGS} ${CMAKE_SHARED_LINKER_FLAGS}" CACHE STRING "" FORCE)

# update RPATH so our custom libraries can be found
# use, i.e. don't skip the full RPATH for the build tree
SET(CMAKE_SKIP_BUILD_RPATH  FALSE)

# when building, don't use the install RPATH already
# (but later on when installing)
SET(CMAKE_BUILD_WITH_INSTALL_RPATH FALSE)

SET(CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}/lib")


# add the automatically determined parts of the RPATH
# which point to directories outside the build tree to the install RPATH
SET(CMAKE_INSTALL_RPATH_USE_LINK_PATH TRUE)

# RE-5093 - The linker on el4 behaves badly, and does not include ld-linux.so by default.
# Because of this, we jump through some hoops below to find and explicitly include ld-linux.so on el4.
IF(EXISTS /etc/redhat-release)
    EXECUTE_PROCESS(COMMAND /bin/fgrep -q " 4." /etc/redhat-release RESULT_VARIABLE ARE_WE_4)
    IF("${ARE_WE_4}" STREQUAL "0")
        EXECUTE_PROCESS(COMMAND /bin/uname -i COMMAND /bin/fgrep -q "x86_64" RESULT_VARIABLE ARE_WE_64_BIT)
        IF("${ARE_WE_64_BIT}" STREQUAL "0")
            SET(LD_SO "/lib64/ld-linux-x86-64.so.2")
        ELSE()
            SET(LD_SO "/lib/ld-linux.so.2")
        ENDIF()

        SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${LD_SO}" CACHE STRING "" FORCE)
    ENDIF()
ENDIF()
